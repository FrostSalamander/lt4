// Entity names
string ENT_CANAL_LOCK = "atdm_froblock_1";
string ENT_CANAL_DOOR = "atdm_mover_door_4";
string ENT_CANAL_SPLINE = "func_splinemover_3";

string ENT_ESCAPE_BOAT = "func_static_1815";

string ENT_BROMWICH = "bromwich";

float OBJ_FINAL = 4;
float OBJ_COMP_BOAT = 1;
float OBJ_COMP_DOOR = 2;
float OBJ_COMP_ESCAPE = 3;

boolean canTakeBoat()
{
	float stateBromwichInBoat = $player1.getObjectiveComp(OBJ_FINAL, OBJ_COMP_BOAT);
	float stateDoor = $player1.getObjectiveComp(OBJ_FINAL, OBJ_COMP_DOOR);

	return (stateBromwichInBoat && stateDoor);
}

// Set the lock to unfrobable once it's opened to prevent unwanted scenarios (door partially open, etc)
void setLockUnfrobable()
{
	sys.getEntity(ENT_CANAL_LOCK).setFrobable(0);
}

void start_spline_end()
{
	if (canTakeBoat())
	{
		// finish the objective
		$player1.setObjectiveComp(OBJ_FINAL, OBJ_COMP_ESCAPE, 1);

		// For some reason (bug?), even though at this point all objective components are complete,
		// and the 'objective complete' notification happens, it's not checked as complete in the UI.
		// Do it ourselves here.
		$player1.setObjectiveState(OBJ_FINAL, OBJ_COMPLETE);

		entity spline		= sys.getEntity(ENT_CANAL_SPLINE);		//the name of the nodraw splinemover brush carrying the curve data in your map
		entity func_mover	= sys.getEntity(ENT_ESCAPE_BOAT);		//the name of the func_mover in your map

		func_mover.enableDamage(0);
		func_mover.time(10);					//let the mover take 10s per lap. Alternatively set speed(), in units per second
		//func_mover.disableSplineAngles();			//optional: use this to stop the mover from rotating wildly depending on how the curve is angled
		sys.threadname("spline_1");				//optional: give this thread a name so the spline mover can be stopped by another script with sys.killthread("spline_1");

		$player1.setOrigin('4972 -760 -636');
		$player1.bind(func_mover);
		func_mover.startSpline(spline);	
	}
}

void start_spline()
{
	entity spline		= $func_splinemover_2;		//the name of the nodraw splinemover brush carrying the curve data in your map
	entity func_mover	= sys.getEntity(ENT_ESCAPE_BOAT);		//the name of the func_mover in your map

	func_mover.time(20);					//let the mover take 10s per lap. Alternatively set speed(), in units per second
	//func_mover.disableSplineAngles();			//optional: use this to stop the mover from rotating wildly depending on how the curve is angled
 	sys.threadname("spline_1");				//optional: give this thread a name so the spline mover can be stopped by another script with sys.killthread("spline_1");

	func_mover.startSpline(spline);	

	//while(1)						//loop the following indefinitely
	//{
	//	func_mover.startSpline(spline);			//start the mover along the spline
	// 	sys.waitFor(func_mover);			//wait for the mover to finish its movement
	//}
}

void attachHandle()
{
	// rotation: -1 0 0 0 0.745451 0.666561 0 0.666561 -0.745451
	// rotate: 0 0 -115
	// origin: 4824.75 -1325.92 -519.284
}

void setHandleFrobable()
{
	sys.println("function called");
	$lever.setFrobable(1);
}

/////////
// LIFT
/////////

// The fetch buttons target both the elevator and a atdm_target_callscriptfunction entity like so:
// The fetch button has a 'target' spawnarg which targets the elevator
// target0 targets the atdm_target_callscriptfunction
// the atdm_target_callscriptfunction calls this script, and targets the doors, and has 'foreach 1' set.
// The button also needs a spawnarg called position_entity_name whose value is the name of the multistate position entity
//
// The elevator needs to have the scriptobject spawnarg set to 'tdm_elevator'
//
// The multistate position entities need to target the doors, and set always_trigger_targets = 0

void controlLiftDoors(entity target, entity button, entity callscriptfunction)
{
    // The button that calls this is targeting the lift with 'target' spawnarg (i.e. not 'target0', 'target1', etc.)
    entity lift = button.getEntityKey("target");

    // Get the multistate position entity this button corresponds to
    // custom spawnarg: didn't want to use 'target' or 'position' because they activate unwanted functionality on some buttons
    entity multiStatePosition = button.getEntityKey("position_entity_name"); 

    // ensure the lift is idle and it's on the appropriate floor
    if ( !lift.isMoving() && (lift.getOrigin() == multiStatePosition.getOrigin()) )
    {
        if (target.IsOpen())
        {
            target.Close();
        }
        else
        {
            target.Open();
        }
    }
}

void main()
{
	sys.waitFrame();

	$bromwich.KO_Knockout($null_entity);
}